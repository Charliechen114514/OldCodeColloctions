;Main Guide Program
;-------------------------------------------------
SECTION MBR vstart=0x7c00
	mov ax, cs
	mov ds, ax
	mov es, ax
	mov ss, ax
	mov fs, ax
	mov sp, 0x7c00 ; 指引调用栈指针指向BIOS加载
;-------------------------------------------------
; INT 0x10	功能号是:0x06，也就是上卷窗口
;-------------------------------------------------
;输入
;AH = 0x06
;AL = 上卷行数（0表示全部上卷）
;BH = 上卷行属性
;(CL, CH) = 窗口左上角的位置
;(DL, DH) = 窗口的右下角位置
; no return

	mov ax, 0x600
	mov bx, 0x700
	mov cx, 0		;左上角
	mov dx, 0x184f	;右下角，VGA文本模式下全屏大小是80个字符呈上25行，下标从0开始，那就是0x 18 4f
	int 0x10		;触发0x10号中断
	
;---------获取光标位置------------------
;.cursor获取当前位置且在光标处打印字符

	mov ah, 3
	mov bh, 0
	
	int 0x10
	
;-------------------------------------
;-----------打印字符串-----------------
	mov ax, message
	mov bp, ax		; es:bp是串的首地址，es跟cs此时一致
	mov cx, 12		;串的长度
	mov ax, 0x1301		;ah al两个8位寄存器被合并赋值，此时这样赋值时ah -> 0x13, al -> 0x01:显示字符串且光标跟随移动
	mov bx, 0x2		;bh存储的是显示的页号，此时显示的是0， bl则是说明字符属性，这里是黑底绿字
	int 0x10		;
;-------------------------------------

	jmp $ ;反复跳转到这一行
	
	message db "Hello,World"
	times 510 - ($ - $$) db 0 ;将剩下的字节处全部赋值为0,留下两个字节给魔数提示
	db 0x55, 0xaa

